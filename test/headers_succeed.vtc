# SPDX-License-Identifier: MIT

varnishtest "Verify that filtered auth backend response headers are passed to the protected backend server."
feature ignore_unknown_macro

server s1 {
    rxreq
    expect req.http.x-user == "admin"
    expect req.http.x-passwd == "<undef>"
    expect req.http.token == "asd"
    expect req.http.other == "<undef>"
    txresp -status 201
} -start

server s_auth_backend {
    rxreq
    txresp \
        -hdr "x-user: admin" \
        -hdr "x-passwd: 123" \
        -hdr "token: asd" \
        -hdr "other: value"
} -start

haproxy h1 -conf {
    global
        lua-load ${testdir}/../auth-request.lua

    listen fe1
        mode http
        bind "fd@${fe1}"
        http-request lua.auth-intercept auth_backend / * * x-user,token -
        http-request deny if ! { var(txn.auth_response_successful) -m bool }
        server s1 ${s1_addr}:${s1_port}

    backend auth_backend
        mode http
        server auth_backend ${s_auth_backend_addr}:${s_auth_backend_port}
} -start

client c1 -connect ${h1_fe1_sock} {
    txreq
    rxresp
    expect resp.status == 201
} -run
