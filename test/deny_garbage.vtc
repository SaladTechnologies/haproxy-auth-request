# SPDX-License-Identifier: MIT

varnishtest "Verify that a backend sending garbage denies access."
feature ignore_unknown_macro

server s1 {
    rxreq
    txresp
} -repeat 7 -start

server s_auth_backend {
    rxreq
    send "foo"
    accept
    rxreq
    send "foo\r\n"
    accept
    rxreq
    send "HTTP/1.0 200 Ok"
    accept
    rxreq
    send "HTTP\r\n"
    accept
    rxreq
    send "HTTP/1\r\n"
    accept
    rxreq
    send "HTTP/1.0 200 Ok\r\n"
    accept
    rxreq
    send "HTTP/1.0 XXX Fail\r\n\r\n"
} -start

haproxy h1 -conf {
    global
        lua-prepend-path ${testdir}/../?/http.lua
        lua-load ${testdir}/../auth-request.lua

    listen fe1
        mode http
        bind "fd@${fe1}"

        http-request lua.auth-request auth_backend /allow
        http-request deny if ! { var(txn.auth_response_successful) -m bool }

        server s1 ${s1_addr}:${s1_port}

    backend auth_backend
        mode http
        server auth_backend ${s_auth_backend_addr}:${s_auth_backend_port}
} -start

client c1 -connect ${h1_fe1_sock} {
    txreq
    rxresp
    expect resp.status == 403
} -repeat 7 -run
